buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.4.2'
    }
}

plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

import proguard.gradle.ProGuardTask

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "kitterhack" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

}

// Add a task to build native libraries before building the jar
tasks.register('buildNative', Exec) {
    workingDir projectDir
    commandLine 'bash', './build_native.sh'
}

// Make processResources depend on native build
processResources.dependsOn buildNative

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    dependsOn buildNative

    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }

    // Explicitly include native libraries in the JAR
    from('src/client/resources') {
        include 'native/**/*.so'
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// ProGuard obfuscation task
tasks.register('proguard', ProGuardTask) {
    dependsOn 'remapJar'

    configuration file('proguard-rules.pro')

    // Input: the remapped JAR from Fabric Loom
    injars "${buildDir}/libs/${project.archives_base_name}-${project.version}.jar"

    // Output: obfuscated JAR
    outjars "${buildDir}/libs/${project.archives_base_name}-${project.version}-obfuscated.jar"

    // Library JARs (Minecraft, Fabric, etc.)
    libraryjars configurations.compileClasspath
    libraryjars "${System.getProperty('java.home')}/jmods"

    // Don't warn about missing classes from Minecraft/Fabric
    dontwarn '**'

    printmapping "${buildDir}/proguard/mapping.txt"
    printseeds "${buildDir}/proguard/seeds.txt"
    printusage "${buildDir}/proguard/usage.txt"
}

// Make build use the obfuscated JAR
build.finalizedBy proguard

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}